/*
 *  This file is part of AndroidAppStudio.
 *
 *  AndroidAppStudio is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  AndroidAppStudio is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *   along with AndroidAppStudio.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
	id("application")
}

// Loading local properties files
def localPropsFile = rootProject.file("local.properties")
def properties = new Properties()

if (localPropsFile.exists()) {
	localPropsFile.withInputStream { inputStream ->
		properties.load(inputStream)
	}
}

application {
	mainClassName = "com.icst.android.appstudio.ExtensionGenerator"
}

task generateExtensions(type: JavaExec) {
	dependsOn("assemble")
	File outputDir = new File("${buildDir}/extensions")
	outputDir.mkdirs()

	String installExtensions = properties.getProperty("installExtensions", "false")
	String isDeveloperMode = properties.getProperty("isDeveloperMode", "false")
	String storage = properties.getProperty("STORAGE", "NOT_PROVIDED")

	classpath = sourceSets.main.runtimeClasspath
	main = "com.icst.android.appstudio.ExtensionGenerator"
	args = [
		outputDir.getAbsolutePath(),
		installExtensions,
		isDeveloperMode,
		storage
	]
}

task generateExtensionsJSONMetadata(type: JavaExec) {
	File outputDir = new File("${buildDir}/extensions/metadata")
	outputDir.mkdirs()

	classpath = sourceSets.main.runtimeClasspath
	main = "com.icst.android.appstudio.ExtensionsMetaDataGenerator"
	args = [outputDir.getAbsolutePath()]
}

dependencies {
	implementation project(":core")
}

sourceSets {
	main {
		java {
			srcDirs = ["src/main/java"]
		}
		resources {
			srcDirs = ["src/main/resources"]
		}
	}
}
