/*
 *  This file is part of Android AppStudio.
 *
 *  Android AppStudio is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  Android AppStudio is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *   along with Android AppStudio.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.application'
}

def localPropsFile = rootProject.file("local.properties")
def properties = new Properties()
if (localPropsFile.exists()) {
    localPropsFile.withInputStream { inputStream ->
        properties.load(inputStream)
    }
}

android {
    namespace 'com.tscodeeditor.android.appstudio'
    compileSdk 34

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getCommitSha = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "HEAD"
            standardOutput = stdout
        }
        return "\"" + stdout.toString().trim() + "\""
    }

    defaultConfig {
        applicationId "com.tscodeeditor.android.appstudio"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0 Alpha01-Snapshot-" + getCommitHash()
        buildConfigField "String", "commitSha", getCommitSha()
        buildConfigField "boolean", "isDeveloperMode", properties.getProperty("isDeveloperMode", "false")

        vectorDrawables { 
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
    
}

dependencies {
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("com.google.android.material:material:1.12.0-alpha03")
    implementation("com.github.bumptech.glide:glide:4.12.0")
    implementation("com.google.code.gson:gson:2.8.7")
    implementation("com.squareup.okhttp3:okhttp:3.9.1")
    implementation project(":block")
}
