plugins {
    id 'com.android.application'
}

def localPropsFile = rootProject.file("local.properties")
def properties = new Properties()
if (localPropsFile.exists()) {
    localPropsFile.withInputStream { inputStream ->
        properties.load(inputStream)
    }
}

android {
    namespace 'com.tscodeeditor.blockly.android'
    compileSdk 33

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getCommitSha = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "HEAD"
            standardOutput = stdout
        }
        return "\"" + stdout.toString().trim() + "\""
    }

    defaultConfig {
        applicationId "com.tscodeeditor.blockly.android"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0 Alpha01-Snapshot-" + getCommitHash()
        buildConfigField "String", "commitSha", getCommitSha()
        buildConfigField "boolean", "isDeveloperMode", properties.getProperty("isDeveloperMode", "false")

        vectorDrawables { 
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
    
}

dependencies {
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("com.google.android.material:material:1.9.0")
}
